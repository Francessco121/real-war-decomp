set shell := ["powershell.exe", "-c"]

# list justfile recipes
@help:
    just --list -u

# recreate build.ninja
@configure:
    dart run ../tools/rw_mod/bin/configure.dart --decomp-root=../ --mod-root=./

# extract function clone binary
@extract:
    dart run ../tools/rw_mod/bin/extract.dart --decomp-root=../ --mod-root=./ --capstone=../tools/capstone.dll

# build new patched exe
[no-exit-message]
@build:
    ninja

# clean + build
[no-exit-message]
@rebuild:
    just clean
    just configure
    ninja

# clean build artifacts
@clean:
    if (Test-Path build -PathType Container) { Remove-Item build -Force -Recurse }
    if (Test-Path game\RealWar.exe -PathType Leaf) { Remove-Item game\RealWar.exe -Force }
    if (Test-Path build.ninja -PathType Leaf) { Remove-Item build.ninja -Force }
    if (Test-Path .ninja_log -PathType Leaf) { Remove-Item .ninja_log -Force }
    if (Test-Path .ninja_deps -PathType Leaf) { Remove-Item .ninja_deps -Force }

# clean build and extract artifacts
@clean-full:
    if (Test-Path bin -PathType Container) { Remove-Item bin -Force -Recurse }
    just clean

# run patched exe
[no-exit-message]
@run *args:
    cd game; ./RealWar.exe {{args}}

# debug patched exe with windows console debugger
[no-exit-message]
@debug *args:
    cd game; &"C:\Program Files (x86)\Windows Kits\10\Debuggers\x86\cdb.exe" -g -G RealWar.exe {{args}}

# build + run
[no-exit-message]
@buildrun *args:
    just build
    just run {{args}}

# rebuild + run
[no-exit-message]
@rebuildrun *args:
    just rebuild
    just run {{args}}

# watch modlog.txt
[no-exit-message]
@logtail:
    Get-Content game/modlog.txt -Wait -Tail 10

# unpack bigfile.dat
[no-exit-message]
@unpack-bigfile:
    dart run ../tools/rw_assets/bin/bigfile.dart unpack -i game/data/bigfile.dat -o game/bigfile -f

# repack new bigfile.dat
[no-exit-message]
@repack-bigfile:
    dart run ../tools/rw_assets/bin/bigfile.dart pack -i game/bigfile -o game/data/bigfile.dat -f

# work with TGC files
[no-exit-message]
@tgc *args:
    dart run ../tools/rw_assets/bin/tgc.dart {{args}}

# work with KVAG files
[no-exit-message]
@kvag *args:
    dart run ../tools/rw_assets/bin/kvag.dart {{args}}
